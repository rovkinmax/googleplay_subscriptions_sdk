apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName rootProject.ext.targetVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

project.archivesBaseName = 'subscriptions'
group = rootProject.ext.groupId // Maven Group ID for the artifact
def gitUrl = 'https://github.com/Apptilaus/googleplay_subscriptions_sdk.git'
def siteUrl = 'https://apptilaus.com/'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            packaging 'aar'
            groupId rootProject.ext.groupId
            artifactId 'subscriptions'
            version rootProject.ext.targetVersion
            name 'Apptilaus Subscriptions'
            modelVersion = '4.0.0'
            description = 'Apptilaus Subscriptions SDK for Android (Google Play)'
            url = siteUrl
            inceptionYear '2019'
            licenses {
                license {
                    name 'MIT'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'Apptilaus'
                    organization = 'Apptilaus'
                    organizationUrl siteUrl
                }
            }

            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }
    }
}

version = rootProject.ext.targetVersion // Library version

Properties properties = new Properties()
properties.load(project.file('local.properties').newDataInputStream())

bintray {
    // User and ApiKey stored in local.properties
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")

    configurations = ['archives']
    // Package info for Bintray
    pkg {
        repo = 'subscriptions'
        name = 'subscriptions'
        licenses = ['MIT']
        vcsUrl = gitUrl
        publish = true
        version {
            gpg {
                sign = false
            }
            mavenCentralSync { // this is optional
                //[Default: true] Determines whether to sync the version to Maven Central.
                sync = false
            }
        }
    }
}

// Dependency to call only bintrayUpload task
bintrayUpload.dependsOn install

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
artifacts {
    archives sourcesJar
}